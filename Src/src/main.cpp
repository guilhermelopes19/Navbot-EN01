//Robot controlled header file
#include <SimpleFOC.h>
#include <Arduino.h>
//#include <MPU6050_tockn.h>
// #include "Servo_STS3032.h"
//#include <MPU9250_asukiaaa.h>
#include <IMU.h>
#include <BitBang_I2C.h>

//WiFi transmission header file
#include <WebSocketsServer.h>
#include <ArduinoJson.h>
#include <WebServer.h>
#include <WiFi.h>
#include <FS.h>
#include "basic_web.h"
#include "robot.h"
#include "wifi_robot.h"
#include "esp_adc_cal.h"
#include "ble.h"
#include "EEPROM.h"
#include "eeprom_util.h"
#include "feedback_util.h"
#include "web_socket_client_util.h"
 
//#include "cpu0_task.h"

#define SERVOMIN 75
#define SERVOMAX 550

/************Instance definition*************/

void lqr_balance_loop();
void robot_sitdown();
void leg_loop();
void jump_loop();
void yaw_loop();
void web_loop();
void yaw_angle_addup();
void basicWebCallback(void);
void webSocketEventCallback(uint8_t num, WStype_t type, uint8_t* payload, size_t length);
void adc_calibration_init();
void bat_check();
void bat_led_blink();
bool one_second_tick(void);
bool ten_msec_tick(void);
uint16_t angleToPulse(int ang);

//Electromotor instance
BLDCMotor motor1 = BLDCMotor(7);
BLDCMotor motor2 = BLDCMotor(7);
BLDCDriver3PWM driver1 = BLDCDriver3PWM(32, 33, 25, 22);
BLDCDriver3PWM driver2 = BLDCDriver3PWM(26, 27, 14, 12);

//Encoder instance
TwoWire I2Cone = TwoWire(0);
TwoWire I2Ctwo = TwoWire(1);
MagneticSensorI2C sensor1 = MagneticSensorI2C(AS5600_I2C);
MagneticSensorI2C sensor2 = MagneticSensorI2C(AS5600_I2C);

//PID instance
/*PIDController pid_angle{ .P = 1, .I = 0, .D = 0, .ramp = 100000, .limit = 8 };
PIDController pid_gyro{ .P = 0.5, .I = 0, .D = 0, .ramp = 100000, .limit = 8 };
PIDController pid_distance{ .P = 0, .I = 0, .D = 0, .ramp = 100000, .limit = 8 };
PIDController pid_speed{ .P = 0, .I = 0, .D = 0, .ramp = 100000, .limit = 8 };
PIDController pid_yaw_angle{ .P = 1.0, .I = 0, .D = 0, .ramp = 100000, .limit = 8 };
PIDController pid_yaw_gyro{ .P = 0.04, .I = 0, .D = 0, .ramp = 100000, .limit = 8 };
PIDController pid_lqr_u{ .P = 1, .I = 15, .D = 0, .ramp = 100000, .limit = 8 };
PIDController pid_zeropoint{ .P = 0.002, .I = 0, .D = 0, .ramp = 100000, .limit = 4 };
PIDController pid_roll_angle{ .P = 8, .I = 0, .D = 0, .ramp = 100000, .limit = 450 };*/

PIDController pid_angle{ .P = 30, .I = 0, .D = 0, .ramp = 100000, .limit = 8 };
PIDController pid_gyro{ .P = 2, .I = 0, .D = 0, .ramp = 100000, .limit = 8 };
PIDController pid_distance{ .P = 0, .I = 0, .D = 0, .ramp = 100000, .limit = 8 };
PIDController pid_speed{ .P = 0, .I = 0, .D = 0, .ramp = 100000, .limit = 8 };
PIDController pid_yaw_angle{ .P = 0, .I = 0, .D = 0, .ramp = 100000, .limit = 8 };
PIDController pid_yaw_gyro{ .P = 0, .I = 0, .D = 0, .ramp = 100000, .limit = 8 };
PIDController pid_lqr_u{ .P = 1, .I = 15, .D = 0, .ramp = 100000, .limit = 8 };
PIDController pid_zeropoint{ .P = 0.002, .I = 0, .D = 0, .ramp = 100000, .limit = 4 };
PIDController pid_roll_angle{ .P = 0, .I = 0, .D = 0, .ramp = 100000, .limit = 450 };

//Low pass filter instance
LowPassFilter lpf_joyy{ .Tf = 0.2 };
LowPassFilter lpf_zeropoint{ .Tf = 0.1 };
LowPassFilter lpf_roll{ .Tf = 0.3 };

// Commander instance
Commander command = Commander(Serial);

void StabAngle(char* cmd) {
  command.pid(&pid_angle, cmd);
}
void StabGyro(char* cmd) {
  command.pid(&pid_gyro, cmd);
}
void StabDistance(char* cmd) {
  command.pid(&pid_distance, cmd);
}
void StabSpeed(char* cmd) {
  command.pid(&pid_speed, cmd);
}
void StabYawAngle(char* cmd) {
  command.pid(&pid_yaw_angle, cmd);
}
void StabYawGyro(char* cmd) {
  command.pid(&pid_yaw_gyro, cmd);
}
void lpfJoyy(char* cmd) {
  command.lpf(&lpf_joyy, cmd);
}
void StabLqrU(char* cmd) {
  command.pid(&pid_lqr_u, cmd);
}
void StabZeropoint(char* cmd) {
  command.pid(&pid_zeropoint, cmd);
}
void lpfZeropoint(char* cmd) {
  command.lpf(&lpf_zeropoint, cmd);
}
void StabRollAngle(char* cmd) {
  command.pid(&pid_roll_angle, cmd);
}
void lpfRoll(char* cmd) {
  command.lpf(&lpf_roll, cmd);
}

bool sitdown_falg = false;

//void Stabtest_zeropoint(char* cmd) { command.pid(&test_zeropoint, cmd); }

//WebServer instance
WebServer webserver;                                // server
WebSocketsServer websocket = WebSocketsServer(81);  // Define a webSocket server to process messages sent by clients
int joystick_value[2];

// MPU9250 instance
IMU imu(&I2Ctwo);

BBI2C bbi2c; 

// Função que configura o PCA9685 manualmente nos pinos 21 e 4
void inicializarPCA9685() {
  // 1. Configura os pinos do I2C
  memset(&bbi2c, 0, sizeof(bbi2c));
  bbi2c.bWire = 0; // 0 significa usar os pinos digitais (BitBang)
  bbi2c.iSDA = 21; // Seu pino SDA
  bbi2c.iSCL = 4;  // Seu pino SCL
  I2CInit(&bbi2c, 100000); // Velocidade de 100kHz

  // 2. Configura o PCA9685 para servos (50Hz)
  uint8_t sleep_cmd[2] = {0x00, 0x10}; // Coloca o chip em sleep para mudar a frequência
  I2CWrite(&bbi2c, 0x40, sleep_cmd, 2);

  uint8_t prescale_cmd[2] = {0xFE, 121}; // Define a frequência para ~50Hz
  I2CWrite(&bbi2c, 0x40, prescale_cmd, 2);

  uint8_t wake_cmd[2] = {0x00, 0x00}; // Acorda o chip
  I2CWrite(&bbi2c, 0x40, wake_cmd, 2);
  delay(5);

  uint8_t mode_cmd[2] = {0x00, 0xA1}; // Liga o Auto-Incremento para escrita rápida
  I2CWrite(&bbi2c, 0x40, mode_cmd, 2);
}

// Função que substitui a 'board1.setPWM' da Adafruit
void setPWM_Software(uint8_t servo_num, uint16_t pulse) {
  uint8_t base_reg = 0x06 + (4 * servo_num); // Descobre o registrador do motor desejado
  
  // Monta o pacote de dados (inicia em 0, termina no valor do pulso)
  uint8_t data[5];
  data[0] = base_reg;
  data[1] = 0x00;                 // Tempo ON LSB
  data[2] = 0x00;                 // Tempo ON MSB
  data[3] = pulse & 0xFF;         // Tempo OFF LSB
  data[4] = (pulse >> 8) & 0xFF;  // Tempo OFF MSB
  
  // Envia via I2C de Software
  I2CWrite(&bbi2c, 0x40, data, 5);
}

/************parameter definition*************/
#define pi 3.1415927

//LQR Self-balancing controller parameters
float LQR_angle = 0;
float LQR_gyro = 0;
float LQR_speed = 0;
float LQR_distance = 0;
float angle_control = 0;
float gyro_control = 0;
float speed_control = 0;
float distance_control = 0;
float LQR_u = 0;
float angle_zeropoint = 1.62 * DEG2RAD;//-1.48;
float distance_zeropoint = -256.0;  //Wheel position shift zero offset \
 (-256 is an impossible displacement value, use it as a sign that it is not refreshed)


//YAW axis control data
float YAW_gyro = 0;
float YAW_angle = 0;
float YAW_angle_last = 0;
float YAW_angle_total = 0;
float YAW_angle_zero_point = -10;
float YAW_output = 0;

//Leg steering gear control data
/*byte ID[2];
s16 Position[2];
u16 Speed[2];
byte ACC[2];*/

//Logical processing flag bit
float robot_speed = 0;          //Record the current wheel speed
float robot_speed_last = 0;     //Record the wheel speed at the previous time
int wrobot_move_stop_flag = 0;  //Record the sign that the rocker stops
int jump_flag = 0;              //Jump period identification
float leg_position_add = 0;     //roll axis balance control quantity

//Voltage detection

static const adc1_channel_t channel = ADC1_CHANNEL_7;
// static const adc_bits_width_t width = ADC_WIDTH_BIT_12;
static const adc_atten_t atten = ADC_ATTEN_DB_11;
static const adc_unit_t unit = ADC_UNIT_1;

//Power display LED Pin
#define LED_BAT 13

void user_function(char* cmd) {
  if (*cmd == '{') {
    rp.json_test(cmd);
  }
}

void setup() {
  Serial.begin(2000000);
  Serial2.begin(1000000);
  rp.get_pcb_version();
  rp.config_json_init();
  ble_init();
  wifi_init();
  if (get_wifi_state() != WIFI_STATE.CLOSE) {
    webserver.begin();
    webserver.on("/", HTTP_GET, basicWebCallback);
    websocket.begin();
    websocket.onEvent(webSocketEventCallback);
  }
  

  web_sockets_client_init();

  //xTaskCreatePinnedToCore(cpu0_task, "cpu0_task", 2048, NULL, 0, NULL, 0);

  //Steering gear initialization
  //Steering gear effective stroke 450
  //Left-hand steering gear[2048+12+50,2048+12+450]
  //Right-hand steering gear[2048-12-50,2048-12-450]
  /*sms_sts.pSerial = &Serial2;
  ID[0] = 1;
  ID[1] = 2;
  ACC[0] = 30;
  ACC[1] = 30;
  Speed[0] = 300;
  Speed[1] = 300;
  Position[0] = 2048;
  Position[1] = 2048;*/
  //The steering gear (ID1/ID2) runs to their respective positions at maximum speed V=2400 steps/SEC and \
  acceleration A=50(50*100 steps/SEC ^2)
  //sms_sts.SyncWritePosEx(ID, 2, Position, Speed, ACC);

  //Voltage detection
  adc_calibration_init();
  adc1_config_width(ADC_WIDTH_BIT_12);
  adc1_config_channel_atten(channel, atten);
  esp_adc_cal_characterize(unit, atten, ADC_WIDTH_BIT_12, 0, &adc_chars);

  //Voltage detection LED
  pinMode(LED_BAT, OUTPUT);

  // Encoder setup
  I2Cone.begin(19, 18, 400000UL);
  I2Ctwo.begin(23, 5, 400000UL);
  sensor1.init(&I2Cone);
  sensor2.init(&I2Ctwo);

  // ==========================================
  // MPU9250 setup & Calibração Manual
  // ==========================================
  //mpu9250.setWire(&I2Ctwo);
  //mpu9250.calibrate();
  imu.calibrate();
  // ==========================================

  //Connect the motor object to the encoder object
  motor1.linkSensor(&sensor1);
  motor2.linkSensor(&sensor2);

  //Speed loop PID parameter
  motor1.PID_velocity.P = 0.05;
  motor1.PID_velocity.I = 1;
  motor1.PID_velocity.D = 0;

  motor2.PID_velocity.P = 0.05;
  motor2.PID_velocity.I = 1;
  motor2.PID_velocity.D = 0;

  // Motor driver setup
  motor1.voltage_sensor_align = 6;
  motor2.voltage_sensor_align = 6;
  driver1.voltage_power_supply = 8;
  driver2.voltage_power_supply = 8;
  driver1.init();
  driver2.init();

  //Connect the motor object to the drive object
  motor1.linkDriver(&driver1);
  motor2.linkDriver(&driver2);

  motor1.torque_controller = TorqueControlType::voltage;
  motor2.torque_controller = TorqueControlType::voltage;
  motor1.controller = MotionControlType::torque;
  motor2.controller = MotionControlType::torque;

  // Motor-related Settings
  motor1.useMonitoring(Serial);
  motor2.useMonitoring(Serial);
  //init
  motor1.init();
  motor1.initFOC();
  motor2.init();
  motor2.initFOC();

  rp.m1_direction = motor1.sensor_direction == CW?  -0.5 : 0.5;
  rp.m2_direction = motor2.sensor_direction == CCW? -0.5 : 0.5;

  Serial.println("Motor1 direction:" + String(rp.m1_direction));
  Serial.println("Motor2 direction:" + String(rp.m2_direction));

  // Setup pca9685
  inicializarPCA9685();
  
  // Map motor to commander
  command.add('A', StabAngle, (char*) "pid angle");
  command.add('B', StabGyro, (char*) "pid gyro");
  command.add('C', StabDistance, (char*) "pid distance");
  command.add('D', StabSpeed, (char*) "pid speed");
  command.add('E', StabYawAngle, (char*) "pid yaw angle");
  command.add('F', StabYawGyro, (char*) "pid yaw gyro");
  command.add('G', lpfJoyy, (char*) "lpf joyy");
  command.add('H', StabLqrU, (char*) "pid lqr u");
  command.add('I', StabZeropoint, (char*) "pid zeropoint");
  command.add('J', lpfZeropoint, (char*) "lpf zeropoint");
  command.add('K', StabRollAngle, (char*) "pid roll angle");
  command.add('L', lpfRoll, (char*) "lpf roll");
  command.add('U', user_function, (char*) "user function");

  leg_loop(); 

  //command.add('M', Stabtest_zeropoint, "test_zeropoint");
  delay(500);
}

void loop() {

  if (one_second_tick()) {
    bat_check();  //Voltage detection
    wifi_loop();
    static int ttss;
    //Serial.println(ttss++);
  }
  if (ten_msec_tick()) {
    ble_loop();
    bat_led_blink();  //Voltage indication LED light
    rp.test_log_output();
    Serial.print("Angle y: ");
    Serial.println(imu.getAngleY() * RAD2DEG);
  }

  web_loop();  //Web data update

  imu.updateMPU();    //IMU data update

  

  lqr_balance_loop();  //lqr self-balancing control
  yaw_loop();          //yaw axis steering control

  //The self-balancing calculated output torque is assigned to the motor
  motor1.target = -(LQR_u + YAW_output) * rp.m1_direction;
  motor2.target = -(LQR_u - YAW_output) * rp.m2_direction;
  //motor1.target = (LQR_u + YAW_output) * rp.m1_direction;
  //motor2.target = (LQR_u - YAW_output) * rp.m2_direction;
 
  

  //Shut down output after falling out of control
  
  if (abs(LQR_angle) > rp.uncontrollable_angle) {
    rp.uncontrollable = 1;
  }
  if (rp.uncontrollable != 0)  //Delay recovery after lifting
  {
    if (abs(LQR_angle) < rp.recovery_angle) {
      rp.uncontrollable++;
    }
    if (rp.uncontrollable > 200)  //The delay time of 200 program cycles
    {
     rp.uncontrollable = 0;
    }
  }

  
  if(wrobot.go == 0){
    //robot_sitdown();
  }else{
    sitdown_falg = false;
  }

  //Turn off output (remote control stop or Angle is too large out of control)
  if (rp.uncontrollable != 0) {
    sitdown_falg = true;
    motor1.target = 0;
    motor2.target = 0;
    leg_position_add = 0;
    wrobot.joyx = 0;
    wrobot.joyy = 0;
    wrobot.roll = 0;
    wrobot.height = 70;
  }

  //Record the last remote control data
  wrobot.dir_last = wrobot.dir;
  wrobot.joyx_last = wrobot.joyx;
  wrobot.joyy_last = wrobot.joyy;

  //The FOC phase voltage is computed iteratively
  motor1.loopFOC();
  motor2.loopFOC();

  //Set the wheel motor output
  motor1.move();
  motor2.move();

  command.run();
}

//lqr self-balancing control
void lqr_balance_loop() {
  /*
  * LQR balance formula, in order to facilitate the adjustment of parameters in actual use, 
  * the formula is decomposed into 4 P controls, 
  * using the PIDController method in commander real-time debugging
  */
  //QR_u = LQR_k1*(LQR_angle - angle_zeropoint) + LQR_k2*LQR_gyro + LQR_k3*(LQR_distance - distance_zeropoint) + LQR_k4*LQR_speed;

  //The negative value is given because, according to the current motor wiring, the positive torque will turn backwards
  LQR_distance = -(motor1.shaft_angle * rp.m1_direction + motor2.shaft_angle * rp.m2_direction);
  LQR_speed    = -(motor1.shaft_velocity * rp.m1_direction + motor2.shaft_velocity * rp.m2_direction);

  // PATCH A: inicializa distance_zeropoint no primeiro ciclo real
  /*static bool distance_initialized = false;
  if (!distance_initialized) {
      distance_zeropoint = LQR_distance;
      distance_initialized = true;
      return; // pula só o primeiríssimo ciclo da vida do robô
  }*/

  LQR_angle = imu.getAngleY();
  LQR_gyro = imu.getGyroYRads();
  angle_control = pid_angle(LQR_angle - angle_zeropoint);
  gyro_control = pid_gyro(LQR_gyro);

  //Motion detail optimization processing
  if (wrobot.joyy != 0)  //Handling when there are forward and backward direction motion instructions
  {
    distance_zeropoint = LQR_distance;  //Displacement zero reset
    pid_lqr_u.error_prev = 0;           //The output integral is cleared to zero
  }

  if ((wrobot.joyx_last != 0 && wrobot.joyx == 0) || (wrobot.joyy_last != 0 && wrobot.joyy == 0))  //Stop in place processing when motion instruction returns to zero
  {
    wrobot_move_stop_flag = 1;
  }
  if ((wrobot_move_stop_flag == 1) && (abs(LQR_speed) < 0.5)) {
    distance_zeropoint = LQR_distance;  //Displacement zero reset
    wrobot_move_stop_flag = 0;
  }

  if (abs(LQR_speed) > 15)  //Stop in place treatment when being pushed rapidly
  {
    distance_zeropoint = LQR_distance;  //Displacement zero reset
  }

  //Calculate displacement control output
  distance_control = pid_distance(LQR_distance - distance_zeropoint);
  speed_control = pid_speed(LQR_speed - 0.1 * lpf_joyy(wrobot.joyy));

  //Wheel lift detection
  robot_speed_last = robot_speed;  //Record two consecutive wheel speeds
  robot_speed = LQR_speed;
  if (abs(robot_speed - robot_speed_last) > 10 || abs(robot_speed) > 50 || (jump_flag != 0)) /*If the angular speed and angular acceleration of the 
                                                    wheel are too large or in the recovery period after jumping, 
                                                    it is considered that the wheel is off the 
                                                    ground and needs special treatment.
                                                  */
  {
    distance_zeropoint = LQR_distance;     //Displacement zero point reset
    LQR_u = angle_control + gyro_control;  //When the wheel part is off the ground, \
                                          the quantity of the opposite wheel part is not output.\
                                          Conversely, under normal conditions, the balanced torque is fully output
    pid_lqr_u.error_prev = 0;              //The output integral is reset to zero
  } else {
    LQR_u = angle_control + gyro_control + distance_control + speed_control;
  }

  //Trigger conditions: No signal input from the remote control, \
    normal intervention of the wheel position movement control, \
    and not in the recovery period after jumping
  if (abs(LQR_u) < 5 && wrobot.joyy == 0 && abs(distance_control) < 4 && (jump_flag == 0)) {

    LQR_u = pid_lqr_u(LQR_u);  //Compensate for the nonlinearity of small torque
    angle_zeropoint -= pid_zeropoint(lpf_zeropoint(distance_control));  //Center of gravity adaptive
  } else {
    pid_lqr_u.error_prev = 0;  //The output integral is reset to zero
  }

  //The balance control parameters are adaptive
  if(pid_speed.P > 0) {
    if (wrobot.height < 50) {
      pid_speed.P = 0.7;
    } else if (wrobot.height < 64) {
      pid_speed.P = 0.6;
    } else {
      pid_speed.P = 0.5;
    }
  }
}
void robot_sitdown()
{
  if(rp.uncontrollable != 0)
  { 
    return;
  }
  
  float _angle = LQR_angle - angle_zeropoint;
  //当前没有坐下，调整角度
  if(  _angle > (-1) && sitdown_falg == false )
  {
    wrobot.joyy = -100;
  }
  //此时身体已经为后仰，可以关闭动力，然后自然坐下
  else{
    sitdown_falg = true;
    motor1.target = 0;
    motor2.target = 0;
    leg_position_add = 0;
    wrobot.joyx = 0;
    wrobot.joyy = 0;
    wrobot.roll = 0;
    wrobot.height = 32;
  }
}
//Leg movement control
void leg_loop() {

  setPWM_Software(0, angleToPulse(0));
  setPWM_Software(1, angleToPulse(180));
  setPWM_Software(2, angleToPulse(180));
  setPWM_Software(3, angleToPulse(0));

  /*jump_loop();
  if (jump_flag == 0)  //Not in a jumping state
  {
    static s16 last_Position[2];
    //Adaptive control of the body height
    ACC[0] = 5;
    ACC[1] = 5;
    Speed[0] = 150;
    Speed[1] = 150;
    float roll_angle = (float)mpu6050.getAngleX() + rp.offset_roll;
    // leg_position_add += pid_roll_angle(roll_angle);
    leg_position_add = pid_roll_angle(lpf_roll(roll_angle));  //test
    Position[0] = 2048 + 8.4 * (wrobot.height - 32) - leg_position_add;
    Position[1] = 2048 - 8.4 * (wrobot.height - 32) - leg_position_add;
    if (Position[0] < 2110)
      Position[0] = 2110;
    else if (Position[0] > 2510)
      Position[0] = 2510;
    if (Position[1] < 1586)
      Position[1] = 1586;
    else if (Position[1] > 1986)
      Position[1] = 1986;

    if((last_Position[1] != Position[1])  || (last_Position[0] != Position[0]))
    {
      last_Position[0] = Position[0];
      last_Position[1] = Position[1];
      sms_sts.SyncWritePosEx(ID, 2, Position, Speed, ACC);
    }
    
  }*/
}

//Jump control
void jump_loop() {
  /*if ((wrobot.dir_last == STOP) && (wrobot.dir == JUMP) && (jump_flag == 0)) {
    ACC[0] = 0;
    ACC[1] = 0;
    Speed[0] = 0;
    Speed[1] = 0;
    Position[0] = 2048 + 12 + 8.4 * (80 - 32);
    Position[1] = 2048 - 12 - 8.4 * (80 - 32);
    sms_sts.SyncWritePosEx(ID, 2, Position, Speed, ACC);

    jump_flag = 1;
  }
  if (jump_flag > 0) {
    jump_flag++;
    if ((jump_flag > 30) && (jump_flag < 35)) {
      ACC[0] = 0;
      ACC[1] = 0;
      Speed[0] = 0;
      Speed[1] = 0;
      Position[0] = 2048 + 12 + 8.4 * (40 - 32);
      Position[1] = 2048 - 12 - 8.4 * (40 - 32);
      sms_sts.SyncWritePosEx(ID, 2, Position, Speed, ACC);

      jump_flag = 40;
    }
    if (jump_flag > 200) {
      jump_flag = 0;  //Ready to jump again
    }
  }*/
}

//yaw axis steering control
void yaw_loop() {
  //YAW_output = 0.03*(YAW_Kp*YAW_angle_total + YAW_Kd*YAW_gyro);
  yaw_angle_addup();

  YAW_angle_total += wrobot.joyx * 0.002;
  float yaw_angle_control = pid_yaw_angle(YAW_angle_total);
  float yaw_gyro_control = pid_yaw_gyro(YAW_gyro);
  YAW_output = yaw_angle_control + yaw_gyro_control;

}

//Web Data Update
void web_loop() {
  webserver.handleClient();
  websocket.loop();
  web_sockets_client_loop();  // The websocket client continuously makes requests.
  rp.spinOnce();              //Update the control information returned by the web end
}

//The yaw axis Angle accumulation function
void yaw_angle_addup() {
  YAW_angle = imu.getAngleZ();
  YAW_gyro = imu.getGyroZRads();

  if (YAW_angle_zero_point == (-10)) {
    YAW_angle_zero_point = YAW_angle;
  }

  float yaw_angle_1, yaw_angle_2, yaw_addup_angle;
  if (YAW_angle > YAW_angle_last) {
    yaw_angle_1 = YAW_angle - YAW_angle_last;
    yaw_angle_2 = YAW_angle - YAW_angle_last - 2 * PI;
  } else {
    yaw_angle_1 = YAW_angle - YAW_angle_last;
    yaw_angle_2 = YAW_angle - YAW_angle_last + 2 * PI;
  }

  if (abs(yaw_angle_1) > abs(yaw_angle_2)) {
    yaw_addup_angle = yaw_angle_2;
  } else {
    yaw_addup_angle = yaw_angle_1;
  }

  YAW_angle_total = YAW_angle_total + yaw_addup_angle;
  YAW_angle_last = YAW_angle;
}

void basicWebCallback(void) {
  webserver.send(300, "text/html", basic_web);
}

void webSocketEventCallback(uint8_t num, WStype_t type, uint8_t* payload, size_t length) {
  if (type == WStype_TEXT) {
    String payload_str = String((char*)payload);
    StaticJsonDocument<300> doc;
    DeserializationError error = deserializeJson(doc, payload_str);

    String mode_str = doc["mode"];
    if (mode_str == "basic") {
      rp.parseBasic(doc);
    }
  }
}
//Voltage detection initialization
void adc_calibration_init() {
  if (esp_adc_cal_check_efuse(ESP_ADC_CAL_VAL_EFUSE_TP) == ESP_OK) {
    printf("eFuse Two Point: Supported\n");
  } else {
    printf("eFuse Two Point: NOT supported\n");
  }
  //Check Vref is burned into eFuse
  if (esp_adc_cal_check_efuse(ESP_ADC_CAL_VAL_EFUSE_VREF) == ESP_OK) {
    printf("eFuse Vref: Supported\n");
  } else {
    printf("eFuse Vref: NOT supported\n");
  }
}

//Voltage detection
void bat_check() {
  static uint16_t bat_check_num = 2;
  if (bat_check_num > 2) {
    //Voltage reading
    uint32_t sum = 0;
    sum = analogRead(BAT_PIN);
    uint32_t voltage = esp_adc_cal_raw_to_voltage(sum, &adc_chars);
    rp.battery_voltage = (voltage * 4) / 1000.0;

    //Serial.println(rp.battery_voltage);
    //Battery display
    bat_check_num = 0;
  } else
  {
    bat_check_num++;
  }
}
void bat_led_blink() {
  // Blink pattern:
  // - voltage <= 7.2V : LED off (insufficient)
  // - voltage > 8.0V  : 5 blinks (full)
  // - 7.2V < voltage <= 8.0V : linear map to 1..4 blinks
  // Sequence: short on/off blinks (200ms on, 200ms off) repeated N times, then pause between sequences.
  static uint8_t target_blinks = 0;       // how many blinks to emit in the next sequence
  static uint8_t blink_index = 0;         // current blink number in the running sequence
  static bool in_sequence = false;        // are we currently emitting a sequence
  static bool led_on = false;             // current LED state within sequence
  static unsigned long last_ts = 0;       // last timestamp for toggle
  static unsigned long seq_pause_ts = 0;  // timestamp when sequence finished (for pause timing)

  const unsigned long blink_on_ms = 200;
  const unsigned long blink_off_ms = 200;
  const unsigned long pause_between_sequences_ms = 1500;

  // determine target_blinks from voltage
  float v = rp.battery_voltage;
  if (v <= 7.2f) {
    target_blinks = 0; // off
  } else if (v > 8.0f) {
    target_blinks = 5; // full
  } else {
    float ratio = (v - 7.2f) / (8.0f - 7.2f); // 0..1
    // map ratio to 1..4
    uint8_t mapped = 1 + (uint8_t)(ratio * 4.0f);
    if (mapped > 4) mapped = 4;
    target_blinks = mapped;
  }

  unsigned long now = millis();

  // if no blinks required, ensure LED off and reset state
  if (target_blinks == 0) {
    digitalWrite(LED_BAT, LOW);
    in_sequence = false;
    blink_index = 0;
    led_on = false;
    seq_pause_ts = now;
    return;
  }

  // If not in sequence, wait for pause then start
  if (!in_sequence) {
    if (now - seq_pause_ts >= pause_between_sequences_ms) {
      in_sequence = true;
      blink_index = 0;
      led_on = false;
      last_ts = now;
    } else {
      digitalWrite(LED_BAT, LOW);
      return;
    }
  }

  // In sequence: toggle LED according to durations
  if (!led_on) {
    // currently off; time to turn on for next blink?
    if (now - last_ts >= blink_off_ms) {
      // start ON period
      digitalWrite(LED_BAT, HIGH);
      led_on = true;
      last_ts = now;
    }
  } else {
    // currently on; check if ON period elapsed
    if (now - last_ts >= blink_on_ms) {
      digitalWrite(LED_BAT, LOW);
      led_on = false;
      last_ts = now;
      blink_index++;
      if (blink_index >= target_blinks) {
        // finished sequence
        in_sequence = false;
        seq_pause_ts = now;
        blink_index = 0;
      }
    }
  }
}

//Generate a one-second tick
bool one_second_tick(void) {
  static unsigned long lastMillis = 0;
  unsigned long currentMillis = millis();
 
  if (currentMillis - lastMillis >= 1000) {
    lastMillis = currentMillis;
    return 1;
  }
  //Overflow handling
  if (lastMillis > currentMillis) {
    lastMillis = currentMillis;
  }

  return 0;
}
//milliseconds
bool ten_msec_tick(void) {
  static unsigned long lastMillis = 0;
  unsigned long currentMillis = millis();

  if (currentMillis - lastMillis >= 10) {
    lastMillis = currentMillis;
    return 1;
  }
  //Overflow handling
  if (lastMillis > currentMillis) {
    lastMillis = currentMillis;
  }

  return 0;
}

uint16_t angleToPulse(int ang){
  int pulse = map(ang,0, 180, SERVOMIN,SERVOMAX);
  return pulse;
}
